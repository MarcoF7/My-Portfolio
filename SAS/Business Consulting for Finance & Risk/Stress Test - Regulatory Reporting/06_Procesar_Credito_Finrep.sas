/*** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***/

/* PROGRAMA: 06_Procesar_Credito_Finrep.sas
/* OBJETIVO: Relizar el tratamiento para homologar la base Finrep a nivel contrato
/* AUTOR:	 Management Solutions
/* PROCESO:  Se realiza el tratamiento de las cuentas de crédito
/*
/* 			 DATASETS GENERADOS:
/* 			 ...
/*Ceconta
/* FECHA: 01/03/2016
/*
/* NOTAS: No aplica
/*
/* LOG DE CAMBIOS:
/* FECHA	AUTOR	Objetivo
/* ...
/*** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***/

/*******************************
 *********************************/

/*PASO 1: SE SELECCIONA EL PERIMETRO DE STRESS TEST*/
/*PASO 1.1: SE ALMACENA LAS CUENTAS NEOCON FALLIDAS*/
PROC SQL;
	CREATE TABLE H_CUENTAS_ELIMINAR_CREDITO_FA AS
		SELECT *
			FROM &BIBLIOTECA..H_CUENTAS_ELIMINAR_CREDITO	
				WHERE(DESCRIPCION = "Fallidos");
QUIT;

/*PASO 1.2: SE RETIRA LAS CUENTAS NEOCON QUE NO ENTRAN AL PERIMETRO STRESS TEST,
SE SELECCIONA EL SALDO CONTABLE BRUTO Y LOS CAMPOS DE INTERES*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CREDITO_PERIMETRO_STRESS AS
		SELECT A.PERIODO,A.SOCIEDAD_INFORMANTE,CONTRATO,"" AS CECONTA, CUENTA_LOCAL,
			A.CUENTA_NEOCON,SECTOR,FINALIDAD,TIPOLOGIA,TIPO_DE_INTERES,SOCIEDAD_INTERGRUPO,
			POR_TIPO_DE_GARANTIA,
			NOMBRE_DEL_IMPORTE,DIVISA,CORASU,
			SUM(SALDO_CONTABLE) AS SALDO_CONTABLE
		FROM 
			&BIBLIOTECA..CREDITO_FINREP A
		WHERE ( 
			(Cuenta_Neocon NOT IN (select Cuenta_NEOCON from H_CUENTAS_ELIMINAR_CREDITO_fa))
			AND
			(NOMBRE_DEL_IMPORTE="Saldo contable (bruto)"))
		GROUP BY 1,2,3,4,5,6,7,8,9,10,11,12,13,14,15;
QUIT;

/*PASO 2: SE CONTRAVALORA A EUROS*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CREDITO_DIVISA AS
		SELECT A.*,(A.SALDO_CONTABLE/B.TIPO_CAMBIO) AS SALDO_EUROS
			FROM &BIBLIOTECA..CREDITO_PERIMETRO_STRESS A,
				(SELECT TIPO_CAMBIO FROM &BIBLIOTECAINPUTS..TIPO_DE_CAMBIO WHERE DIVISA="EUR") B;
QUIT;

/*PASO 3: SE HOMOLOGA EL EPIGRAFE*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CREDITO_EPIGRAFE AS
		SELECT A.*,B.'EPIGRAFE LOCAL'N FROM 
			&BIBLIOTECA..CREDITO_DIVISA A
		LEFT JOIN Epigrafe_CContable_Format_dis B
			ON A.CUENTA_LOCAL=B.CUENTA;
QUIT;

/*PASO 4: SE HOMOLOGA EL PERIODO Y ENTIDADAD(G001-G003)*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CREDITO_G001_G003 AS
		SELECT A.*,"&FECHAREPORTE" AS G001,A.SOCIEDAD_INFORMANTE AS G002,
			"02" AS G003
		FROM &BIBLIOTECA..CREDITO_EPIGRAFE A;
QUIT;

/*PASO 5: SE HOMOLOGA EL SEGEMENTO FINREP(G004)*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CREDITO_G004 AS
		SELECT A.*,B.VALOR AS G004, B.NUEVO_SECTOR AS NUEVO_SECTOR FROM
			&BIBLIOTECA..CREDITO_G001_G003 A
		LEFT JOIN &BIBLIOTECA..H_SECTOR_PRESTAMOS B
			ON COMPRESS(UPCASE(A.SECTOR))=COMPRESS(UPCASE(B.SECTOR)) AND 
			COMPRESS(UPCASE(A.TIPOLOGIA))=COMPRESS(UPCASE(B.TIPOLOGIA));
QUIT;

/*PASO 6: ADEMAS SE DEBE IDENTIFICAR LOS BANCOS MULTILATERALES DE DESARROLLO DE FINREP,
ESTAN DENTO DE ENTIDADES DE CREDITO Y SE DISTINGUEN POR SU CORASU: M43*/
DATA PREVIOG007;
	SET &BIBLIOTECA..CREDITO_G004;

	IF (SECTOR = "Entidades de credito") THEN
		DO;
			IF (CORASU = "M43") THEN
				NUEVO_SECTOR = "MDB";
		END;
RUN;

/*PASO 7: SE CALCULA EL CAMPO C0005 (TAMAÑO DE EMPRESA)*/
DATA PREVIOG007_TAMANO;
	SET PREVIOG007;
	FORMAT C0005 $1.;

	IF (G004 = "004") THEN /*SEGMENTO FINREP = "004" (SNF - PYMES)*/
		C0005 = "P";
	ELSE IF (G004 = "005") THEN /*SEGMENTO FINREP = "005" (SNF - RESTO)*/
		C0005 = "G";
RUN;

/*PASO 8: SE NECESITA EL CAMPO C0005 EN CREDITO LIQUIDEZ*/
PROC SQL;
	CREATE TABLE TIPOLOGIA_CONTRATO AS
		SELECT DISTINCT CONTRATO, C0005
			FROM PREVIOG007_TAMANO;
QUIT;

/*PASO 9: SE CALCULA EL CAMPO C0973 (MARCA HIPOTECARIO FINREP)*/
DATA PREVIOG007_MARCAHIP;
	SET PREVIOG007_TAMANO;

	IF (POR_TIPO_DE_GARANTIA = "Garantia inmobiliaria comercial") THEN
		C0973 = "C";
	ELSE IF (POR_TIPO_DE_GARANTIA = "Garantia inmobiliaria residencial") THEN
		C0973 = "R";
	ELSE C0973 = "N"; /*NO APLICA GARANTIA INMOBILIARIA*/
RUN;

/*PASO 10: SE NECESITA EL CAMPO C0973 EN LIQUIDEZ CREDITO*/
PROC SQL;
	CREATE TABLE MARCAHIP_CONTRATO AS
		SELECT CONTRATO, MAX(C0973) AS C0973
			FROM PREVIOG007_MARCAHIP
				GROUP BY 1;
QUIT;

/*PASO 11: ENRIQUECIMIENTO CON INVENTARIOS MIS*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CREDITO_G007_G009 AS
		SELECT G001,G002,G003,G004,A.CONTRATO,CUENTA_LOCAL,NUEVO_SECTOR,
			A.CUENTA_NEOCON,FINALIDAD,TIPO_DE_INTERES,
			SOCIEDAD_INTERGRUPO,POR_TIPO_DE_GARANTIA,NOMBRE_DEL_IMPORTE,
			DIVISA,A.SALDO_EUROS,'EPIGRAFE LOCAL'N AS G009,B.TIPOTAE,
			INDICADOR_NEW_BUSSINES,PERRENOV,TIPOTASA,
			INDIREF,CLASREF,TIPOREF,FEVENCIM,VENCIMIENTO_ORIGINAL,
			C0005,C0973,
			A.SALDO_EUROS AS SALDO_CONTABLE_TMP,
			((B.Saldo_Medio*ABS(A.SALDO_EUROS))/SUM(ABS(A.SALDO_EUROS))) AS SALDO_MEDIO_PREV,
			((B.Ingresos_Gastos*ABS(A.SALDO_EUROS))/SUM(ABS(A.SALDO_EUROS))) AS INGRESOS_GASTOS

		FROM PREVIOG007_MARCAHIP A LEFT JOIN 
			&BIBLIOTECA..INVENTARIO_CONTRATOS_SALDOS2 B
			ON A.CONTRATO=B.CONTRATO 

			GROUP BY A.CONTRATO

		ORDER BY A.CONTRATO,
			B.TIPOTAE,B.FEVENCIM;
QUIT;






/*PASO 12: SE HOMOLOGA SI HAY INTERCOMPANY(G012)*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CREDITO_G012 AS
		SELECT A.*,
			CASE 
				WHEN SOCIEDAD_INTERGRUPO^="Tercero"
				THEN "INT" 
			ELSE "TER" 
			END 
		AS G012
			FROM &BIBLIOTECA..CREDITO_G007_G009 A;
QUIT;

/*PASO 13: SE HOMOLOGA EL PRODUCTO_PRESTAMOS(G013)*/
/*PASO 13.1: PRIMERO SE CALCULA EL TIPO DE PRODUCTO PARA HIPOTECAS*/
PROC SQL;
	CREATE TABLE PARAM_TIPO_DE_GARANTIA AS 
		SELECT POR_TIPO_DE_GARANTIA, VALOR
			FROM &BIBLIOTECA..H_PRODUCTO_PRESTAMOS_FIN
				WHERE(FINALIDAD="Viviendas");
QUIT;

/*PASO 13.2: HOMOLOGAMOS CREDITOS HIPOTECARIOS
SE USA EL CAMPO POR_TIPO_DE_GARANTIA*/
PROC SQL;
	CREATE TABLE CREDITO_G013_PREV AS
		SELECT A.*, 
			CASE 
				WHEN B.VALOR^="" THEN B.VALOR
				ELSE "03" 
			END 
		AS G013,
			"" AS G014,
			"" AS G015
		FROM
			&BIBLIOTECA..CREDITO_G012 A
		LEFT JOIN 
			PARAM_TIPO_DE_GARANTIA B
			ON 
			COMPRESS(UPCASE(A.POR_TIPO_DE_GARANTIA))
			=COMPRESS(UPCASE(B.POR_TIPO_DE_GARANTIA))
			AND COMPRESS(A.POR_TIPO_DE_GARANTIA)
			=COMPRESS("Garantia inmobiliaria residencial");
QUIT;

/*PASO 13.3: AHORA SE CALCULA EL TIPO DE PRODUCTO PARA CONSUMO,
SE USAN LOS CAMPOS POR_TIPO_DE_GARANTIA Y FINALIDAD.
FINALMENTE EL RESTO SERA RESTO*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CREDITO_G013 
		(DROP = G013 RENAME=(G013_NEW = G013)) AS
			SELECT A.*,
				CASE 
					WHEN B.VALOR^="" THEN B.VALOR
					ELSE A.G013 
				END 
			AS G013_NEW
				FROM
					CREDITO_G013_PREV A
				LEFT JOIN 
					&BIBLIOTECA..H_PRODUCTO_PRESTAMOS_FIN B
					ON COMPRESS(UPCASE(A.FINALIDAD))
					=COMPRESS(UPCASE(B.FINALIDAD)) AND 
					COMPRESS(UPCASE(A.POR_TIPO_DE_GARANTIA))
					=COMPRESS(UPCASE(B.POR_TIPO_DE_GARANTIA)) AND
					COMPRESS(UPCASE(A.FINALIDAD))=COMPRESS("CREDITO AL CONSUMO");
QUIT;

/*PASO 14: AHORA SE NECESITA REPLICAR ESTA LOGICA EN 
CREDITO LIQUIDEZ,
ENTONCES SE LLEVA A NIVEL CONTRATO LOS RESULTADOS*/
PROC SQL;
	CREATE TABLE PROD_PREST_FINREP_CRED AS
		SELECT CONTRATO, MAX(G013) AS G013
			FROM &BIBLIOTECA..CREDITO_G013
				GROUP BY 1;
QUIT;

/*PASO 15: SE HOMOLOGA LA DIVISA*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CREDITO_G016 AS
		SELECT A.*,
			CASE 
				WHEN B.VALOR NE '' THEN B.VALOR 
				ELSE "XXX" 
			END 
		AS G016
			FROM &BIBLIOTECA..CREDITO_G013 A
				LEFT JOIN &BIBLIOTECA..H_DIVISA B
					ON A.DIVISA=B.VALOR;
QUIT;

/*PASO 16: SE HOMOLOGA EL TIPO DE TASA*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CREDITO_G017_G018 AS
		SELECT A.*,
			CASE 
				WHEN FINALIDAD="Viviendas" THEN "F"
				ELSE B.G017 
			END 
		AS G017,
			"N" AS G018
		FROM &BIBLIOTECA..CREDITO_G016 A
			LEFT JOIN &BIBLIOTECA..H_TIPO_TASA B
				ON A.TIPO_DE_INTERES=B.TIPO_DE_INTERES;
QUIT;

/*PASO 17: SE HOMOLOGA EN BUCKET LA TASA DE INTERES*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CREDITO_INTERES AS 
		SELECT 
			A.*,
		CASE 
			WHEN TIPOTAE=.
			THEN "" 
		ELSE 
		B.TIPO_TRAMO 
		END 
	AS G019
		FROM
			&BIBLIOTECA..CREDITO_G017_G018 A
		LEFT JOIN
			&BIBLIOTECA..H_TRAMOS_TIPO B
			ON
			(A.TIPOTAE)>B.COTA_INFERIOR 
			AND 
			(A.TIPOTAE)<=B.COTA_SUPERIOR;
QUIT;

/*PASO 18: SE HOMOLOGA EL INDICE DE REFERENCIA*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CREDITO_G021 AS 
		SELECT A.*,
			CASE 
				WHEN G017="V" THEN 
				B.VALOR 
				ELSE "" 
			END 
		AS G021
			FROM
				&BIBLIOTECA..CREDITO_INTERES A
			LEFT JOIN &BIBLIOTECA..H_CURVA_FINREP B ON
				A.CLASREF=B.CURVA AND A.PERRENOV=B.PERRENOV AND A.DIVISA=B.DIVISA;
QUIT;

/*PASO 19: SE HOMOLOGA EL VALOR DE REFERENCIA G022*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CREDITO_G022 AS 
		SELECT 
			A.*,
		CASE 
			WHEN (G017="V" AND A.TIPOREF^=.) THEN 
			B.TIPO_TRAMO 
			ELSE "" 
		END 
	AS G022
		FROM
			&BIBLIOTECA..CREDITO_G021 A
		LEFT JOIN
			&BIBLIOTECA..H_TRAMOS_TIPO B
			ON
			(A.TIPOREF)>B.COTA_INFERIOR 
			AND 
			(A.TIPOREF)<=B.COTA_SUPERIOR;
QUIT;

/*PASO 20: SE HOMOLOGA EL SPREAD G023*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CREDITO_G023 AS 
		SELECT 
			A.*,
		CASE 
			WHEN (G017="V" AND (A.TIPOTAE-A.TIPOREF)^=.)
			THEN 
		B.TIPO_TRAMO 
		ELSE "" 
		END 
	AS G023
		FROM
			&BIBLIOTECA..CREDITO_G022 A
		LEFT JOIN
			&BIBLIOTECA..H_TRAMOS_TIPO B
			ON
			((A.TIPOTAE-A.TIPOREF))>B.COTA_INFERIOR 
			AND 
			((A.TIPOTAE-A.TIPOREF))<=B.COTA_SUPERIOR;
QUIT;

/*PASO 21: SE HOMOLOGA EL PERIODO DE REVISION G024*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CREDITO_G024 AS
		SELECT A.*,B.VALOR AS  G024
			FROM &BIBLIOTECA..CREDITO_G023 A
				LEFT JOIN &BIBLIOTECA..H_REVISION B
					ON A.PERRENOV=B.PERRENOV;
QUIT;

/*PASO 22: SE HOMOLOGA EL INDICADOR NEW BUSSINES Y DEFAULT (G025-G026)*/
DATA &BIBLIOTECA..CREDITO_G025_G026;
	SET &BIBLIOTECA..CREDITO_G024;
	G025=INDICADOR_NEW_BUSSINES;
RUN;

/*PASO 23: SE LLEVA EL SEGMENTO FINREP (G004) DE FINREP A LIQUIDEZ*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..SECTOR_CONTRATO_CREDITOFIN AS
		SELECT DISTINCT CONTRATO,G004 FROM
			&BIBLIOTECA..CREDITO_G025_G026;
QUIT;

/*PASO 24: SE LLEVA EL SECTOR DE LA CONTRAPARTE FINREP-LIQUIDEZ 
(NUEVO_SECTOR) DE FINREP A LIQUIDEZ*/
PROC SQL;
	CREATE TABLE NUEVO_SECTOR_CONTRATO_CREDITOFIN AS
		SELECT DISTINCT CONTRATO, NUEVO_SECTOR
			FROM &BIBLIOTECA..CREDITO_G025_G026;
QUIT;

/*PASO 25: SE AGRUPA LAS VARIABLES Y SE HOMOLOGA A001-A003*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CREDITO_AGRUPADO_CONTRATO AS
		SELECT CONTRATO,
			G001,G002,G003,G004,NUEVO_SECTOR,"" AS G005,"" AS G006,
			G009,"" AS G010,"" AS G011,G012,G013,"" AS G014,"" AS G015,
			G016,G017,G018,G019,G021,G022,G023,G024,G025,CUENTA_NEOCON,CUENTA_LOCAL, TIPOTAE,FEVENCIM,
			PERRENOV,CLASREF,TIPOREF,VENCIMIENTO_ORIGINAL,
			SOCIEDAD_INTERGRUPO AS INTERCOMPANY,
			DIVISA, C0005,C0973,
			SALDO_CONTABLE_TMP AS SALDO_EUROS,
			SALDO_MEDIO_PREV AS SALDO_MEDIO,
			INGRESOS_GASTOS
		FROM &BIBLIOTECA..CREDITO_G025_G026;
QUIT;

/*PASO 26: SE COMPLETAN POR 0 LOS CAMPOS DE SALDO Y SE DEJA LA MARCA*/
DATA &BIBLIOTECA..CREDITO_MARCA_SALDO_CONTRATO;
	SET &BIBLIOTECA..CREDITO_AGRUPADO_CONTRATO;

	IF SALDO_EUROS=. THEN
		DO;
			SALDO_EUROS=0;
			MARCA_SALDO_EUROS="S";
		END;

	IF SALDO_MEDIO=. THEN
		DO;
			SALDO_MEDIO=0;
			MARCA_SALDO_MEDIO="S";
		END;

	IF INGRESOS_GASTOS=. THEN
		DO;
			INGRESOS_GASTOS=0;
			MARCA_INGRESOS_GASTOS="S";
		END;

	IF 
		VENCIMIENTO_ORIGINAL<0 THEN
		DO;
			VENCIMIENTO_ORIGINAL=.;
		END;
RUN;

/*PASO 27: SE AGRUPA Y SUMARIZA LOS SALDOS*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CREDITO_SALDO_CONTRATO AS
		SELECT CONTRATO,G001,G002,G003,G004,G005,G006,
			G009,G010,G011,G012,G013,G014,G015,
			G016,G017,G018,G019,G021,G022,G023,G024,G025,CUENTA_NEOCON,NUEVO_SECTOR,TIPOTAE,FEVENCIM,
			PERRENOV,CLASREF,TIPOREF,
			VENCIMIENTO_ORIGINAL,
			INTERCOMPANY,
			DIVISA,
			MARCA_SALDO_EUROS,
			MARCA_SALDO_MEDIO,
			MARCA_INGRESOS_GASTOS,CUENTA_LOCAL,C0005,C0973,
			SUM(SALDO_EUROS) AS SALDO_EUROS ,
			SUM(SALDO_MEDIO)  AS SALDO_MEDIO,
			SUM(INGRESOS_GASTOS) AS INGRESOS_GASTOS
		FROM &BIBLIOTECA..CREDITO_MARCA_SALDO_CONTRATO
			GROUP BY 1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,
				25,26,27,28,29,30,31,32,33,34,35,36,37,38,39;
QUIT;

/*PASO 28: SE CALCULAN LAS VARIABLES A001_A009*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CREDITO_CONTRATO_A001_A009 AS
		SELECT CONTRATO,G001,G002,G003,G004,G005,G006,
			G009,G010,G011,G012,G013,G014,G015,
			G016,G017,G018,G019,G021,G022,G023,G024,G025,CUENTA_NEOCON,CUENTA_LOCAL,NUEVO_SECTOR,
			PERRENOV,TIPOTAE,TIPOREF,
			VENCIMIENTO_ORIGINAL,
			INPUT(FEVENCIM,YYMMDD8.) AS FEVENCIM FORMAT = DATE9.,
			INTERCOMPANY,
			VENCIMIENTO_ORIGINAL*SALDO_EUROS AS VENCIMIENTO_AUX,
			MARCA_SALDO_MEDIO,
			MARCA_INGRESOS_GASTOS, C0005,C0973,
			SALDO_EUROS AS A001,
			SALDO_MEDIO AS A002,
			INGRESOS_GASTOS AS A009
		FROM &BIBLIOTECA..CREDITO_SALDO_CONTRATO;
QUIT;

/*PASO 30: SE HOMOLOGA LOS CAMPOS G027 Y G028*/
PROC SQL;
	CREATE TABLE &BIBLIOTECA..CONTRATO_FLUJOS_G027_G028 AS
		SELECT CONTRATO,CUENTA_NEOCON, CUENTA_LOCAL, NUEVO_SECTOR, TIPOTAE, TIPOREF, FEVENCIM, INTERCOMPANY,
			G001,G002,G003,G004,G005,G006,G009,G010,G011,G012,
			G013,G014,G015,G016,G017,G018,G019,G021,A001, A002, A009,
			G022,G023,G024,G025,C0005,C0973,
			"" AS G028, "" AS G030
		FROM
			&BIBLIOTECA..CREDITO_CONTRATO_A001_A009 A;
QUIT;

/*PASO 31: SE HOMOLOGA SPREAD, TIPO EMISION FINREP*/
DATA CR_FINREP1;
	SET &BIBLIOTECA..CONTRATO_FLUJOS_G027_G028;
	SPREAD = TIPOTAE - TIPOREF;
	C1014 = ""; /*TIPO DE EMISIÓN FINREP VACIO PORQUE NO ESTAMOS EN EMISIONES*/
RUN;

/*PASO 32: SE CARGA LA FECHA DE BAJA DE LOS INVENTARIOS MIS*/
PROC SQL;
	CREATE TABLE CR_FINREP2 AS
		SELECT A.*, B.FECANCEL 
			FROM CR_FINREP1 A LEFT JOIN INVENTARIOS_MIS_SALDO_FECANCEL B
				ON A.CONTRATO =  B.CONTRATO AND A.CUENTA_LOCAL = B.CTA_PRINCIPAL_TEXTO;
QUIT;

/*PASO 33: SE CARGA LA FECHA DE APERTURA DE LOS INVENTARIOS MIS*/
PROC SQL;
	CREATE TABLE CR_FINREP3 AS
		SELECT A.*, B.FEAPERTU
			FROM CR_FINREP2 A LEFT JOIN INVENTARIO_MIS_SALDO_2 B
				ON A.CONTRATO = B.CONTRATO;
QUIT;

/*PASO 34: SE DA FORMATO A LAS FECHAS*/
data CR_FINREP4
	(
	DROP=FECANCEL FEAPERTU
	RENAME=(FECANCEL2 = FECANCEL FEAPERTU2= FEAPERTU G003=INSTRUMENTO G004 = SEGMENTO_FINREP G013 = PROD_PRESTAMOS G014 = PROD_DEPOSITOS
	G015 = PROD_EMIRF G025 = INDNEWBUS));
	SET CR_FINREP3;

	IF (FECANCEL = "00010101") THEN
		FECANCEL = "";
	FECANCEL2 = INPUT(FECANCEL,YYMMDD8.);
	FORMAT FECANCEL2 DATE10.;

	IF (FEAPERTU = "00010101") THEN
		FEAPERTU = "";
	FEAPERTU2 = INPUT(FEAPERTU,YYMMDD8.);
	FORMAT FEAPERTU2 DATE10.;
RUN;

/*PASO 35: SE HOMOLOGA LOS INDICADORES MATURING BUSINESS Y DE OPERACION VIVA*/
DATA CR_FINREP5 (RENAME=(FEVENCIM = FECHA_VENCIMIENTO FEAPERTU=FECHA_APERTURA));
	SET CR_FINREP4;
	FORMAT INDOPERVIVA $1.;
	FORMAT INDMATBUS $1.;
	INDOPERVIVA = "V";
	ANIODATOS = SUBSTR(G001,1,4);

	IF(ANIODATOS = YEAR(FECANCEL)) THEN
		DO;
			INDMATBUS = "S";
			A001 = 0; /*LAS OPERATIVAS VENCIDAS NO DEBEN TENER SALDO PUNTUAL A CIERRE DE MES*/
			INDOPERVIVA = "F";
		END;
	ELSE INDMATBUS = "N";

	/*COMPLETO LOS CAMPOS FALTANTES*/
	FE_REV = .;
	ANIOAPER = PUT(YEAR(FEAPERTU),4.);

	/*	HAY ALGUNOS INDICADORES NEW BUSINESS VACIOS, NO CRUZARON CON INVENTARIOS MIS, */
	IF (INDNEWBUS = "") THEN
		DO;
			IF(ANIODATOS = ANIOAPER) THEN
				INDNEWBUS = "S";
			ELSE INDNEWBUS = "N";
		END;

	/*SI ALGUN TIPO FIJO/VARIABLE QUEDO VACIO, SERA FIJO*/
	IF (G017 = "") THEN
		G017 = "F";

	
	DIAS = FEVENCIM - FEAPERTU;

RUN;

/*PASO 36: SE SELECCIONA LOS CAMPOS EXIGIDOS A NIVEL CONTRATO*/
proc sql;
	create table CRFinrep_Salida_Final_2017_contr as
		select Contrato as C0000 format = $18.,Nuevo_Sector as C0001 format=$25., C0005,
			A001 as C0016, G001 as C0019,
			G002 as C0020, Intercompany as C0021, G016 as C0022, G017 as C0023, G018 as C0024, G019 as C0025,
			tipotae as C0026, G021 as C0027, TipoRef as C0028, SPREAD as C0029,
			G024 as C0030, fe_rev as C0038, Fecha_apertura as C0039, FECANCEL as C0040,
			A002 as C0041, Fecha_Vencimiento as C0050, G023 as C0052, 
			A009 as C0067, . as C0068,
			"PE" as C0900, "PEN" as C0901, C0973, G028 as C1010 format = $2., C1014,

			Cuenta_Neocon as Cuenta_NEOCON, Cuenta_Local as cta_contable,

			. as PlazoRef, Instrumento, Segmento_FINREP, Prod_Prestamos, Prod_Depositos, Prod_EmiRF, 
			"" as Prod_Derivados format = $2., IndNewBus, IndOperViva, IndMatBus, dias, G012 as Marca_Intergrupo
		from  

			CR_FINREP5;
quit;



/*Puedo ver que la cuenta contable "151509012200000" es la que se repite casi siempre,
Los contratos de FINREP si estan en inventarios MIS pero no con esta cuenta contable de
FINREP, entonces no cruzan bien. 
Y si cruzo solo por contrato sumando los saldos en INV MIS, me traigo un margen demasiado alto
para el valor del saldo puntual y encima me sobrepaso del MARGEN QUE ESPERA HOLDING DEL F16.
Por ahora lo estoy dejando en 0 al margen, pero mejor deberia ver con Leo las bases para
calcular el margen para estas operaciones de tenerlo*/

/*Borramos todas las tablas intermedias que ya no se usan para liberar espacio en la memoria*/
%borrarTabla(&BIBLIOTECA..CREDITO_PERIMETRO_STRESS);
%borrarTabla(&BIBLIOTECA..CREDITO_DIVISA);
%borrarTabla(&BIBLIOTECA..CREDITO_EPIGRAFE);
%borrarTabla(&BIBLIOTECA..CREDITO_G001_G003);
%borrarTabla(&BIBLIOTECA..CREDITO_G004);
%borrarTabla(PREVIOG007);
%borrarTabla(PREVIOG007_TAMANO);
%borrarTabla(PREVIOG007_MARCAHIP);
%borrarTabla(&BIBLIOTECA..CREDITO_G007_G009);
%borrarTabla(&BIBLIOTECA..CREDITO_G012);
%borrarTabla(PARAM_TIPO_DE_GARANTIA);
%borrarTabla(CREDITO_G013_PREV);
%borrarTabla(&BIBLIOTECA..CREDITO_G013);
%borrarTabla(&BIBLIOTECA..CREDITO_G016);
%borrarTabla(&BIBLIOTECA..CREDITO_G017_G018);
%borrarTabla(&BIBLIOTECA..CREDITO_INTERES);
%borrarTabla(&BIBLIOTECA..CREDITO_G021);
%borrarTabla(&BIBLIOTECA..CREDITO_G022);
%borrarTabla(&BIBLIOTECA..CREDITO_G023);
%borrarTabla(&BIBLIOTECA..CREDITO_G024);
%borrarTabla(&BIBLIOTECA..CREDITO_G025_G026);
%borrarTabla(&BIBLIOTECA..CREDITO_AGRUPADO_CONTRATO);
%borrarTabla(&BIBLIOTECA..CREDITO_MARCA_SALDO_CONTRATO);
%borrarTabla(&BIBLIOTECA..CREDITO_SALDO_CONTRATO);
%borrarTabla(&BIBLIOTECA..CREDITO_CONTRATO_A001_A009);
%borrarTabla(&BIBLIOTECA..CONTRATO_FLUJOS_G027_G028);
%borrarTabla(CR_FINREP1);
%borrarTabla(CR_FINREP2);
%borrarTabla(CR_FINREP3);
%borrarTabla(CR_FINREP4);
%borrarTabla(CR_FINREP5);