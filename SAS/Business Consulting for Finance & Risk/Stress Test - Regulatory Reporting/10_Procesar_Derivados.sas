
/* PASO 1: SE PROCESA LOS IMPORTES DE VALOR RAZONABLE 
PARA PODER DISTRIBUIR EL NOCIONAL ENTRE DERIVADO ACTIVO Y PASIVO*/
DATA _NULL_;
SET &BIBLIOTECA..VALOR_RAZONABLE;

	IF (CONCEPTO = "1.2.1 DERIVADOS DE ACTIVO") THEN
		DO;
			CALL SYMPUT('PORC_DER_ACT', TRIM(RATIO));
		END;
	ELSE IF (CONCEPTO = "1.2.2 DERIVADOS DE PASIVO") THEN
		DO;
			CALL SYMPUT('PORC_DER_PAS', TRIM(RATIO));
		END;

RUN;

/*PASO 2: SE CARGA LOS ANEXOS 8 DE DERIVADOS LIQUIDEZ*/
/*NOCIONAL VIENE EN SOLES EN UNIDADES*/
DATA DERIVADOS_LIQUIDEZ; 
SET &BIBLIOTECA..ANEXOS8_DERIVADOS_LIQ;
RUN;

/*PASO 3: SE QUITAN LOS DUPLICADOS*/
PROC SORT DATA=DERIVADOS_LIQUIDEZ NODUPKEY OUT=DERIVADOS_LIQUIDEZ_NODUP DUPOUT=DUPLIC;
	BY NOCIONAL MONEDA_PACTADA NUMCLIEN TASA_RECIBIDA TASA_ENTREGADA DEAL_STAR 
		FECHA_DE_INICIO FECHA_DE_VENCIMIENTO PRODUCTO INTERCOMPANY CLASE_INT_RECIBIDA CLASE_INT_ENTREGADA;
RUN;

/*PASO 4: SE DUPLICA EL NUMERO DE FILAS PARA TENER LAS 2 PATAS:
LA DE ACTIVO Y LA DE PASIVO*/
/*PASO 4.1: SE HACE UN ORDENAMIENTO PREVIO*/
PROC SORT DATA=DERIVADOS_LIQUIDEZ_NODUP OUT=DERIVADOS_LIQUIDEZ_ORD;
	BY DEAL_STAR MONEDA_PACTADA NUMCLIEN FECHA_DE_INICIO FECHA_DE_VENCIMIENTO 
		PRODUCTO INTERCOMPANY NOCIONAL CLASE_INT_RECIBIDA CLASE_INT_ENTREGADA;
RUN;

/*PASO 4.2: SE HACE UN TRANSPOSE, LA PATA DE ACTIVO SE DIRENCIA DE LA DE PASIVO
POR LA TASA*/
PROC TRANSPOSE DATA=DERIVADOS_LIQUIDEZ_ORD OUT=DERIVADOS_LIQUIDEZ_TRANS;
	VAR TASA_RECIBIDA TASA_ENTREGADA;
	BY DEAL_STAR MONEDA_PACTADA NUMCLIEN FECHA_DE_INICIO FECHA_DE_VENCIMIENTO 
		PRODUCTO INTERCOMPANY NOCIONAL CLASE_INT_RECIBIDA CLASE_INT_ENTREGADA;
RUN;

/*PASO 5: SE HOMOLOGA CAMPOS DE STRESS TEST,
SE DIFERENCIA LA PATA DE ACTIVO DE LA DE PASIVO*/
DATA DERIVADOS_LIQUIDEZ_PARAM (DROP= _NAME_ 
RENAME= (DEAL_STAR = C0000 COL1 = TIPOTAE));
SET DERIVADOS_LIQUIDEZ_TRANS;
	FORMAT C0001 $3.;
	FORMAT C0002 $3.;
	FORMAT MARCA_INTERGRUPO $3.;
				
	TIPOREF = 0;
	SPREAD = COL1 - TIPOREF;
	C0019="&FECHAREPORTE";
	C0020="&SOCIEDAD_INFORMANTE";
	C0001 = "";
	SEGMENTO_FINREP = "";/*NO APLICA A DERIVADOS*/
	PROD_PRESTAMOS = "";
	PROD_DEPOSITOS = "";
	PROD_EMIRF = "";

	INDMATBUS = "N";
	INDOPERVIVA = "V"; 
	ANIODATOS = SUBSTR(C0019,1,4);
	DIAS = FECHA_DE_VENCIMIENTO - FECHA_DE_INICIO;

	
	IF (ANIODATOS = YEAR(FECHA_DE_INICIO)) THEN
		INDNEWBUS = "S";
	ELSE	
		INDNEWBUS = "N";


	IF INTERCOMPANY ='Tercero' THEN
		MARCA_INTERGRUPO= 'TER';
	ELSE  
		MARCA_INTERGRUPO='INT';

	IF (_NAME_ = "Tasa_Recibida") THEN /*ACTIVO*/
		DO;
			NOCIONAL = NOCIONAL * &PORC_DER_ACT; /*TOMO EL % DEL NOCIONAL QUE CORRESPONDE AL ACTIVO*/
			C0002 = "DA";
			C0023 = CLASE_INT_RECIBIDA;
			INSTRUMENTO = "04";
		END;
	ELSE IF (_NAME_ = "Tasa_Entregada") THEN /*PASIVO*/
		DO;
			NOCIONAL = NOCIONAL * &PORC_DER_PAS; /*TOMO EL % DEL NOCIONAL QUE CORRESPONDE AL PASIVO*/
			C0002 = "DP";
			C0023 = CLASE_INT_ENTREGADA;
			INSTRUMENTO = "08";
		END;
RUN;

/*PASO 6: HOMOLOGAMOS EL TRAMO TIPO DE INTERES OPERACION*/
PROC SQL;
	CREATE TABLE DERIVADOS_LIQUIDEZ_PARAM_TAE AS 
		SELECT 
			A.*,
			B.TIPO_TRAMO AS C0025

		FROM
			DERIVADOS_LIQUIDEZ_PARAM A
		LEFT JOIN
			&BIBLIOTECA..H_TRAMOS_TIPO B
			ON
			A.TIPOTAE>B.COTA_INFERIOR 
			AND 
			A.TIPOTAE<=B.COTA_SUPERIOR;
QUIT;

/*PASO 7: HOMOLOGAMOS EL TRAMO DE SPREAD*/
PROC SQL;
	CREATE TABLE DERIVADOS_SPREAD_TRM AS 
		SELECT 
			A.*,
		
		 B.TIPO_TRAMO AS C0052

		FROM
			DERIVADOS_LIQUIDEZ_PARAM_TAE A
		LEFT JOIN
			&BIBLIOTECA..H_TRAMOS_TIPO B
			ON
			A.SPREAD>B.COTA_INFERIOR 
			AND 
			A.SPREAD<=B.COTA_SUPERIOR
;
QUIT;

/*PASO 8: SE CARGA LA CUENTA CONTABLE*/
/*PASO 8.1: DAMOS FORMATO A LA CUENTA CONTABLE*/
DATA CTAS_CONTAB_DERIVADOS2 (KEEP=CONTRATO CTA_PRINCIPAL_TEXTO);
SET &BIBLIOTECA..CTAS_CONTAB_DERIVADOS;
	FORMAT CTA_PRINCIPAL_TEXTO $15.;
	CTA_PRINCIPAL_TEXTO=TRIM(CATS(CUENTA_LOCAL,"0000000000000"));
	WHERE(CONTRATO NE "");
RUN;

/*PASO 8.2: SE SELECCIONA LA CUENTA CONTABLE A NIVEL CONTRATO*/
PROC SQL;
	CREATE TABLE CTAS_CONTAB_DERIVADOS3 AS
		SELECT DISTINCT CONTRATO, CTA_PRINCIPAL_TEXTO
			FROM CTAS_CONTAB_DERIVADOS2;
QUIT;	

/*PASO 8.3: SE ENRIQUECE CON LA CTA CONTABLE*/
PROC SQL;
	CREATE TABLE DERIVADOS_CTA_CONT AS
		SELECT A.*, B.CTA_PRINCIPAL_TEXTO
			FROM DERIVADOS_SPREAD_TRM A LEFT JOIN CTAS_CONTAB_DERIVADOS3 B 
				ON A.C0000=B.CONTRATO;
QUIT;

/*PASO 9: SE CARGA LA CUENTA NEOCON*/
/*PASO 9.1: SE TOMAN LAS CUENTAS NEOCON DE INTERES*/
DATA B29_TODAS_DIVISAS_NEOCON2;
SET &BIBLIOTECA..B29_TODAS_DIVISAS_NEOCON;
	WHERE(CTAGESTIONBBV NOT IN ("00000", "0") AND CTACONTABLE NE "");
RUN;

/*PASO 9.2: SE OBTIENE LA CUENTA NEOCON*/
PROC SQL;
	CREATE TABLE DERIVADOS_NEOCON AS
		SELECT A.*, B.CTAGESTIONBBV AS CUENTA_NEOCON
			FROM DERIVADOS_CTA_CONT A LEFT JOIN B29_TODAS_DIVISAS_NEOCON2 B
				ON A.CTA_PRINCIPAL_TEXTO = B.CTACONTABLE;
QUIT;


/*PASO 10: SE HOMOLOGA EL TIPO DE DERIVADO*/
/*AL RESTO DE DERIVADOS QUE NO SON DE COBERTURA SE LE ASIGNA:
"04" -> NO UTILIZADOS PARA CONTABILIDAD DE COBERTURAS*/
PROC SQL;
	CREATE TABLE DERIVADOS_TIPO_DER AS
		SELECT A.*, (CASE WHEN B.TIPO_DERIVADO NE "" THEN B.TIPO_DERIVADO ELSE "04" END) AS TIPO_DERIVADO
			FROM DERIVADOS_NEOCON A LEFT JOIN &BIBLIOTECA..TIPO_DERIVADOS B
				ON A.C0000 = B.NUM_OPERACION;
QUIT;

/*PASO 11: TRATAMIENTO MARGEN FINANCIERO*/
/*OBTENEMOS EL MARGEN FINANCIERO TOTAL DEL B29 Y LO 
REPARTIMOS PROPORCIONALMENTE AL NOCIONAL PARA 
TENERLO A NIVEL DEAL STAR PARA LOS DERIVADOS QUE
APORTAN AL MARGEN (LOS DE COBERTURA)
EL MARGEN ESTA EN SOLES Y EN UNIDADES
*/
/*PASO 11.1: SE CARGA EL MARGEN FINANCIERO TOTAL*/

PROC SQL;
	CREATE TABLE MARGEN_DERIVADOS AS
		SELECT SUM(MARGEN_ACUMULADO_B29) AS SUM_MARGEN
			FROM &BIBLIOTECA..CTAS_MARGEN_DERIVADOS;
QUIT;

/*PASO 11.2: SE ALMACENA EN UNA MACROVARIABLE*/
DATA _NULL_;
SET MARGEN_DERIVADOS;
	IF (_N_ = 1) THEN
		DO;
			CALL SYMPUT('MARGEN_DERIV_TOT', TRIM(SUM_MARGEN));
		END;
RUN;

/*PASO 11.3: SE CALCULA EL NOCIONAL TOTAL*/
/*MARGEN SOLO APLICA A LOS DERIVADOS DE COBERTURA ("01","02","03")
01 -> COBERTURA DE FLUJOS DE EFECTIVO
02 -> COBERTURA DE VALOR RAZONABLE
03 -> COBERTURA DE INVERSIONES NETAS
*/
PROC SQL;
	CREATE TABLE NOCIONAL_TOT AS
		SELECT SUM(NOCIONAL) AS NOCIONAL_T
			FROM DERIVADOS_TIPO_DER
				WHERE(TIPO_DERIVADO IN ("01","02","03"));
QUIT;

/*PASO 11.4: SE ALMACENA EN UNA MACROVARIABLE*/
DATA _NULL_;
SET NOCIONAL_TOT;
	IF (_N_ = 1) THEN
		DO;
			CALL SYMPUT('NOCIONAL_DERIV_COBER', TRIM(NOCIONAL_T));
		END;
RUN;

/*PASO 11.5: SE CARGAN LOS VALORES CALCULADOS*/
DATA DERIVADOS_MARGEN_B29;
SET DERIVADOS_TIPO_DER;
	MARGEN_TOT = &MARGEN_DERIV_TOT;
	NOCIONAL_COBERT = &NOCIONAL_DERIV_COBER;
RUN;

/*PASO 11.6: SE DISTRIBUYE EL MARGEN PROPORCIONALMENTE AL NOCIONAL*/
DATA DERIVADOS_MARGEN_B29_CAL (DROP=MARGEN_TOT NOCIONAL_COBERT);
SET DERIVADOS_MARGEN_B29;
	IF(TIPO_DERIVADO IN ("01","02","03")) THEN
		DO;
			MARGEN_FINANCIERO = (NOCIONAL/NOCIONAL_COBERT)*MARGEN_TOT;
		END;
	ELSE
		DO;
			MARGEN_FINANCIERO = 0;
		END;
RUN;

/*PASO 12: SE CONTRAVALORA A EUROS*/
PROC SQL;
	CREATE TABLE DERIVADOS_CONTRAVALORA
		AS 

		SELECT A.*,A.NOCIONAL*B.TIPO_CAMBIO_EUR  AS NOCIONAL_EUR, 
				A.MARGEN_FINANCIERO*B.TIPO_CAMBIO_EUR AS MARGEN_FINANCIERO_EUR
			FROM
				DERIVADOS_MARGEN_B29_CAL A
				,
				TIPO_DE_CAMBIO B 
			WHERE COMPRESS(UPCASE(B.DIVISA))='PEN';
QUIT;

/*PASO 13: SE CALCULA EL SALDO MEDIO PONDERADO ANUAL*/
DATA DERIVADOS_SALDO_MEDIO;
SET DERIVADOS_CONTRAVALORA;
	dias_apert_vcto = Fecha_de_vencimiento - Fecha_de_inicio;

	FORMAT FECHCOMPDAT2 DATE10.;
	FORMAT FECHINIMESAP DATE10.;
	/*COMPLETAMOS LA FECHA DE DATOS:*/
	FECHCOMPDAT = CAT(C0019,"31"); 
	FECHCOMPDAT2 = INPUT(FECHCOMPDAT,YYMMDD8.);/*FORMATO FECHA*/ 	
	DIAS = DAY(FECHA_DE_INICIO);
	/*PRIMER DIA DEL MES DE LA FECHA DE APERTURA:*/
	FECHINIMESAP = INTNX('day',FECHA_DE_INICIO,-DIAS) + 1; 

	IF (YEAR(FECHA_DE_INICIO) < YEAR(FECHCOMPDAT2)) THEN 
	/*ESTAS OPERACIONES YA ESTABAN VIVAS DESDE INICIO DE AÑO*/
		DO;
			SALDO_MEDIO_EUR = NOCIONAL_EUR; 
		END;
	ELSE 
	/*PARA LOS QUE RECIEN SE APERTURARON EN EL AÑO DE DATOS, 
	EL SALDO MEDIO SE DEBE CALCULAR PONDERANDO*/
	/*SE PONDERA EL NOCIONAL DURANTE LOS MESES QUE ESTUVO CON VIDA*/
		DO; 
			SALDO_MEDIO_EUR = (NOCIONAL_EUR*(FECHCOMPDAT2 - FECHA_DE_INICIO + 1))/(FECHCOMPDAT2 - FECHINIMESAP + 1);
		END;
RUN;


/*PASO 14: SE SELECCIONA LOS CAMPOS EXIGIDOS A NIVEL CONTRATO*/
proc sql;
	create table DERIVADOS_STRESS_2017_CONTR as  /*9300 filas*/
		select C0000 format = $18., C0001, "" as C0005, . as C0016,  
				C0019, C0020, Intercompany as C0021, 
				Moneda_Pactada as C0022, 
				C0023, 
				"N" as C0024, 
				C0025,
				TipoTae as C0026, 
				"" as C0027, 
				TIPOREF as C0028, 
				SPREAD as C0029,
				"" as C0030, 
				. as C0038 format = date10., 
				Fecha_de_inicio as C0039, 
				. as C0040 format = date10.,
				Saldo_Medio_Eur as C0041,
				Fecha_de_vencimiento as C0050, 
				C0052, 
				Margen_Financiero_Eur as C0067,
				Nocional_Eur as C0068,
				"PE" as C0900, "PEN" as C0901, 
				"" as C0973, 
				"" as C1010 format = $2., 
				"" as C1014, /*Solo aplica a emisiones*/

				cuenta_neocon,Cta_Principal_texto as cta_contable,

			. as PlazoRef,Instrumento, Segmento_FINREP, Prod_Prestamos, Prod_Depositos, Prod_EmiRF, 
			Tipo_Derivado as Prod_Derivados format = $2., IndNewBus, IndOperViva, IndMatBus,
			dias_apert_vcto as  dias, Marca_Intergrupo
		from 

			Derivados_Saldo_Medio ;
quit;